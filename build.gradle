plugins {
    id "java-library"
    id "eclipse"
    id "maven-publish"
}

version = '4.0.4'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://artifacts.psi.ch/artifactory/libs-releases" }
    maven { url "https://artifacts.psi.ch/artifactory/libs-snapshots" }    
}

dependencies {
	compile 'ch.psi:bitshuffle-lz4:1.1.2'
	compile 'org.slf4j:slf4j-api:1.7.26'
	compile 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
        compile 'org.zeromq:jeromq:0.5.1'   
	compile 'net.jpountz.lz4:lz4:1.3.0'
	compile 'org.apache.commons:commons-lang3:3.8.1'
	
	testCompile 'ch.qos.logback:logback-classic:1.2.3'
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task fatJar(type: Jar, dependsOn: jar) {
    classifier 'fat'
    duplicatesStrategy = 'exclude'  
    doFirst {
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } } 
    }
    with jar
}


publishing {
    repositories {
        maven {
            credentials {
                username artifactoryUser 
                password artifactoryPwd
            }
            name = "Artifactory"
            url = artifactoryUrlLibSnap
        }    
        maven {
            credentials {
                username githubUser 
                password githubPwd
            }
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/paulscherrerinstitute/bsread_java"
        }      
    }
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId 'ch.psi'
            artifactId 'bsread'
            version version
        }
    }
}

